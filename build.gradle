apply plugin: 'groovy'

repositories {
    jcenter()
    ivy {
        url 'http://diax.coli.uni-saarland.de/mary/'
        layout 'pattern', {
            artifact '[module]-[revision]-[classifier].[ext]'
        }
    }
}

configurations {
    raw
    ehmm
}

sourceSets.create 'raw'

dependencies {
    raw name: 'cmu_us_slt_arctic', version: '0.95', classifier: 'release', ext: 'tar.bz2'
    ehmm(group: 'de.dfki.mary', name: 'marytts-lang-en', version: '5.1.1') {
        exclude module: 'freetts'
        exclude module: 'freetts-de'
        exclude module: 'freetts-en_us'
        exclude module: 'mwdumper'
        exclude module: 'sgt'
    }
    ehmm(group: 'de.dfki.mary', name: 'marytts-builder', version: '5.1.1') {
        exclude module: 'freetts'
        exclude module: 'freetts-de'
        exclude module: 'freetts-en_us'
        exclude module: 'mwdumper'
        exclude module: 'sgt'
    }
}

processRawResources {
    from configurations.raw
    def packs = []
    filesMatching '*.tar.*', {
        packs << tarTree("$destinationDir/$it.name")
    }
    filesMatching '*.zip', {
        packs << zipTree(it.path)
    }
    doLast {
        packs.each { pack ->
            copy {
                from pack
                into destinationDir
                include(['lab', 'pm', 'mcep', 'utt', 'wav'].collect { "**/*.$it" })
                includeEmptyDirs = false
            }
        }
    }
}

task extractText {
    inputs.files fileTree(sourceSets.raw.output.resourcesDir).include('cmu_us_slt_arctic/festival/utts/*.utt')
    outputs.files inputs.files.collect {
        "${sourceSets.raw.output.resourcesDir}/cmu_us_slt_arctic/text/${it.name.replace('.utt', '.txt')}"
    }
    doLast {
        [inputs.files as List, outputs.files as List].transpose().each { inFile, outFile ->
            inFile.eachLine {
                if (it.startsWith('Features')) {
                    outFile.text = it.split(/("\\"|\\"")/)[1]
                }
            }
        }
    }
}

task processEHMMSources(type: Copy) {
    from 'src/ehmm'
    into "$buildDir/ehmm"
}

task compileEHMM(type: Exec) {
    dependsOn processEHMMSources
    commandLine 'make'
    workingDir "$buildDir/ehmm"
}

task runEHMMLabeler(type: JavaExec) {
    classpath configurations.ehmm
    main 'marytts.tools.voiceimport.EHMMLabeler'
    workingDir "${sourceSets.raw.output.resourcesDir}/cmu_us_slt_arctic"
}
